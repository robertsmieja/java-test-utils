buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.6'
    id "com.jfrog.bintray" version "1.7.3"
    id 'nebula.contacts' version '3.0.1'
    id 'nebula.info-basic' version '3.5.0'
    id 'nebula.info-broker' version '3.5.0'
    id 'nebula.info-jar' version '3.5.0'
    id 'nebula.info-owners' version '3.5.0'
    id 'nebula.info-props' version '3.5.0'
    id 'nebula.info-scm' version '3.5.0'
    id 'nebula.javadoc-jar' version '4.9.1'
    id 'nebula.maven-publish' version '4.9.1'
    id 'nebula.release' version '4.1.1'
    id 'nebula.source-jar' version '4.9.1'
}

group 'com.robertsmieja'

apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api 'org.apache.commons:commons-lang3:3.5'
    api 'org.jetbrains:annotations:15.0'
    api 'org.junit.jupiter:junit-jupiter-api:5.0.0-M3'
    implementation 'org.apache.commons:commons-collections4:4.1'
    testCompile 'org.junit.vintage:junit-vintage-engine:4.12.0-M3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M3'
}

contacts {
    'robertsmieja@robertsmieja.com' {
        moniker 'Robert Smieja'
        github 'robertsmieja'
        role 'owner'
    }
}

/* BinTray config */
tasks.release.dependsOn tasks.bintrayUpload

bintray {
    if (!"$bintrayUser" || !"bintrayKey"){
        throw new MissingPropertyException("Missing bintray credentials!"+
                " User: <$bintrayUser> Key: <$bintrayKey>")
    }

    user = bintrayUser
    key = bintrayKey

    publications = ['nebula']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
//        userOrg = 'nebula'
        name = "${project.name}"
        desc = hasProperty('description') ? description : ''
        websiteUrl = "https://github.com/robertsmieja/${project.name}"
        issueTrackerUrl = "https://github.com/robertsmieja/${project.name}/issues"
        vcsUrl = "https://github.com/robertsmieja/${project.name}.git"
        licenses = ['Apache-2.0']
//        labels = []
        publicDownloadNumbers = true
        attributes = [:]
        version {
            name = project.version
            vcsTag = "${project.version}"
            attributes = [:]
            gpg {
                sign = true
            }
//            mavenCentralSync {
//                user = ''
//                password = ''
//            }
        }
    }
}

/* Build Scan config */
buildScan {
    licenseAgree = 'yes'
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    if (System.getenv('CI')) {
        publishAlways()
        tag 'CI'
    }
}

/* Code Coverage */
jacoco {
    toolVersion = '0.7.9'
}
//TODO Why is afterEvaluate needed?
afterEvaluate {
    jacoco.applyTo(tasks.junitPlatformTest)
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
    executionData = files('build/jacoco/junitPlatformTest.exec')
}

