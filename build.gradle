buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.6'
}

buildScan {
    licenseAgree = 'yes'
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    if (System.getenv('CI')) {
        publishAlways()
        tag 'CI'
    }
}

group 'com.robertsmieja'
version '1.0-SNAPSHOT'

apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api 'org.apache.commons:commons-lang3:3.5'
    api 'org.jetbrains:annotations:15.0'
    api 'org.junit.jupiter:junit-jupiter-api:5.0.0-M3'
    implementation 'org.apache.commons:commons-collections4:4.1'
    testCompile 'org.junit.vintage:junit-vintage-engine:4.12.0-M3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M3'
}

/* Code Coverage */
jacoco {
    toolVersion = '0.7.9'
}
//TODO Why is afterEvaluate needed?
afterEvaluate {
    jacoco.applyTo(tasks.junitPlatformTest)
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
    executionData = files('build/jacoco/junitPlatformTest.exec')
}

/* Maven publishing */

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact javadocJar {
                classifier "javadoc"
            }
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            //url ""
        }
    }
}

/* Additional Jars */
task javadocJar(type: Jar){
    from javadoc
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

artifacts {
    archives jar, javadocJar, sourceJar
}
